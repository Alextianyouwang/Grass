#pragma kernel GlobalWind

RWStructuredBuffer<float> _WindBuffer;
uint _MaxCount;
float _TileSize;
float _TileDimension;
float _CornerX;
float _CornerY;
float _Time;
float _DirX;
float _DirY;
float _Frequency;
float _Speed;
float _Direction;
#include "../../INCLUDE/HL_Noise.hlsl"
#include "../../INCLUDE/HL_GraphicsHelper.hlsl"
[numthreads(1024, 1, 1)]
void GlobalWind(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= _MaxCount)
        return;
    
    int coordX = id.x / _TileDimension;
    int coordY = id.x % _TileDimension;
    float2 posXZ = float2(coordX * _TileSize + _CornerX, coordY * _TileSize + _CornerY);
    posXZ = Rotate2D(posXZ, -_Direction);
    
    float freq = _Frequency;
    float amp = 1;
    float speedRamp = 0;
    float2 offset = float2(_Time * _Frequency * _Speed, 0);
    float noise = 0;
    for (int i = 0; i < 3; i++)
    {
        noise +=  perlinNoise(posXZ * freq - offset, float2(12.9898, 78.233)) * amp;
        freq *= 1.5;
        amp *= 0.5;
        speedRamp += 2;
        offset = float2(_Time * freq * (_Speed + speedRamp), 0);
    }

    _WindBuffer[id.x] =noise ;

}